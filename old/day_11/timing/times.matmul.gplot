# As you see, # are used to create comments (same as in bash)

# customize line styles in advance to use 'linestyle 1' or 'ls 2' in the plot command
set style line 1 lt 1 lw 3 ps 0.5 pt 5 lc rgb 'gray50'	# 1-4 only differ by type (solid, dashed, etc)
set style line 2 lt 2 lw 3 ps 0.5 pt 5 lc rgb 'gray50'
set style line 3 lt 3 lw 3 ps 0.5 pt 5 lc rgb 'gray50'
set style line 4 lt 4 lw 3 ps 0.5 pt 5 lc rgb 'gray50'
set style line 5 lt 1 lw 3 ps 0.5 pt 5 lc rgb 'light-red'
set style line 6 lt 1 lw 3 ps 0.5 pt 7 lc rgb 'dark-chartreuse'
set style line 7 lt 1 lw 3 ps 0.5 pt 9 lc rgb 'royalblue'
set style line 8 lt 1 lw 3 ps 0.5 pt 11 lc rgb 'cyan'

# select output format and file (PDF)
set term pdfcairo color size 3,3 enhanced dashed font 'Deja Vu Sans,10'
set output 'times.matmul.pdf'

set title 'scaling of matrix multiplication'	# title of the whole graph

set tics out				# tickmarks outside axes

set xlabel "n"				# label for x axis
#set xtics rotate
#set logscale x
set xrange [250:10250]		# set x axis limits
set format x '%.0s%c'		# format x axis in 1k, 2k format

set ylabel "runtime (ms)"	# label for left y axis
set yrange [0:4]			# set left y axis limits

set key top left			# turns on legend within the plot

#------------------------------------------------------------
# this is the syntax for fitting data to functions in Gnuplot

log2(x) = log(x)/log(10)	# define a log base 2 function

# define the functions for fitting
fn_ij(x) = a_ij*x				# linear
fn2_ij(x) = b_ij*x**2			# quardatic
flogn_ij(x) = c_ij*log2(x)		# log(n)
fnlogn_ij(x) = d_ij*x*log2(x)	# n log(n)

# fit data from file (columns 1:2) with functions
fit fn_ij(x) 'times.matmul.dat' using 1:2 via a_ij
fit fn2_ij(x) 'times.matmul.dat' using 1:2 via b_ij
fit flogn_ij(x) 'times.matmul.dat' using 1:2 via c_ij
fit fnlogn_ij(x) 'times.matmul.dat' using 1:2 via d_ij

# now the same for columns 1:3
fn_ji(x) = a_ji*x
fn2_ji(x) = b_ji*x**2
flogn_ji(x) = c_ji*log2(x)
fnlogn_ji(x) = d_ji*x*log2(x)

fit fn_ji(x) 'times.matmul.dat' using 1:3 via a_ji
fit fn2_ji(x) 'times.matmul.dat' using 1:3 via b_ji
fit flogn_ji(x) 'times.matmul.dat' using 1:3 via c_ji
fit fnlogn_ji(x) 'times.matmul.dat' using 1:3 via d_ji

# actual plot command
# note how we plot the lines first and the points after
# so that points apper on top of the lines
# notice the use of columns 1:2:4 as source of y coordinates and errorbars
plot fn2_ij(x) with l ls 5 title 'n^2',\
		fn_ij(x) with l ls 1 title 'n',\
		flogn_ij(x) with l ls 2 title 'log(n)',\
		fnlogn_ij(x) with l ls 3 title 'n log(n)',\
		'times.matmul.dat' using 1:2:4 with yerrorbars ls 5 title 't_{row}(n)',\
		fn2_ji(x) with l ls 6 title 'n^2',\
		'times.matmul.dat' using 1:3:5 with yerrorbars ls 6 title 't_{col}(n)'
